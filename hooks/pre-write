#!/usr/bin/perl

use strict;
use warnings;
use FindBin qw($Script);

my $ssh = $ENV{SSH_CLIENT} or die "Only SSH allowed\n";
my $base = $ENV{GIT_DIR} or die "GIT hook ENV malfunction!\n";
my $acl = {};
my $conf = `git config --list`;
while ($conf =~ s/^acl.(\w+)=(.*)$//m) {
    my $param = $1;
    my $keys = $2;
    foreach my $key (split /,/, $keys) {
        $acl->{$param}->{$key} = 1;
    }
}

$SIG{PIPE} = sub { exit 1; };

my $KEY = $ENV{KEY} || "UNKNOWN";
my $ip = $ssh =~ /^([\da-f\.:]+) /i ? $1 : "UNKNOWN";
$conf !~ /^proxy/m or 0 == system "hooks/proxy", $Script or die localtime().": [$KEY\@$ip] git-server: proxy operation failure: $?\n";
# Verify IP
if (my $banned = system "hooks/restrictip") {
    exit $banned;
}
# Cannot pass without writers access:
my $allowed = $acl->{writers}->{$KEY};
warn localtime().": [$KEY\@$ip] git-server: RUNNING PUSH ...\n" if $allowed;

if (my $pid = open my $fh, "|-", "$base/hooks/logger", "") {
    if ($allowed) {
        print $fh localtime().": [$KEY\@$ip]: pushed\n";
    }
    else {
        print $fh localtime().": [$KEY\@$ip]: Missing [acl.writers] permission. Blocked push attempt!\n";
    }
    close $fh;
    die localtime().": [$KEY\@$ip] git-server: SERVER ERROR! log.logfile failed! $?\n" if $?;
}

die localtime().": [$KEY\@$ip] git-server: You cannot push changes. Missing [writers] permissons.\n" unless $allowed;
